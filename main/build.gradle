plugins {
    id 'java-application-conventions'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco-report-aggregation'
}

dependencyManagement {
    imports {
        mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {
    implementation project(':adapter')
    implementation project(':application')
    implementation project(':domain')
    implementation project(':infrastructure')

    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.+'
    implementation 'org.flywaydb:flyway-core'
    implementation 'com.h2database:h2'
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

tasks.named('testCodeCoverageReport', JacocoReport) {
    getClassDirectories().setFrom(files(
            [project(':adapter'),
             project(':domain'),
             project(':application'),
             project(':infrastructure'),
            ].collect {
                it.fileTree(dir: "${it.buildDir}/classes/java/main", exclude: [
                        '**/database/**',
                        '**/remote/**',
                        '**/Application'
                ])
            }
    ))
}
